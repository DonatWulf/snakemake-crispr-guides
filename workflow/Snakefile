# ----------------------------------------------------- #
# A Snakemake workflow for the design of small guide    #
# RNAs (sgRNAs) for CRISPR applications.                #
#                                                       #
# Author: Michael Jahn                                  #
# Date: 2023-04-04                                      #
# License: GPL v3 (for all 3rd party tools              #
# separate licenses may apply)                          #
# ----------------------------------------------------- #


# load rules
# -----------------------------------------------------
include: "rules/common.smk"


# load and print configuration
# -----------------------------------------------------
configfile: "config/config.yml"


# target rule
# -----------------------------------------------------
rule all:
    input:
        "results/versions/log_env.txt",
        "results/module_logs/log.txt",


# module to fetch genome from NCBI or Ensemble
# -----------------------------------------------------
rule get_genome:
    params:
        database=config["get_genome"]["database"],
        assembly=config["get_genome"]["assembly"],
        fasta=config["get_genome"]["fasta"],
        gff=config["get_genome"]["gff"],
    output:
        path=directory("results/get_genome"),
        fasta="results/get_genome/genome.fasta",
        gff="results/get_genome/genome.gff",
    conda:
        "envs/get_genome.yml"
    log:
        path="results/get_genome/log.txt",
    script:
        "scripts/get_genome.py"


# module to build bowtie index
# -----------------------------------------------------
rule bowtie_index:
    input:
        rules.get_genome.output.fasta,
    output:
        path=directory("results/bowtie_index"),
    conda:
        "envs/bowtie_index.yml"
    log:
        path="results/bowtie_index/log.txt",
    shell:
        "bowtie2-build {input} {output.path}/index > {log.path} 2>&1"


# module to design guide RNAs using R crisprDesign
# -----------------------------------------------------
rule design_guides:
    input:
        fasta=rules.get_genome.output.fasta,
        gff=rules.get_genome.output.gff,
    params:
        config["design_guides"],
        max_cores=workflow.cores,
    output:
        guideRNAs_top="results/design_guides/guideRNAs_top.csv",
    conda:
        "envs/design_guides.yml"
    log:
        path="results/design_guides/log.txt",
    script:
        "scripts/design_guides.R"


# module to visualize guide RNA prediction using R
# -----------------------------------------------------
rule visualize_guides:
    input:
        guides=rules.design_guides.output.guideRNAs_top,
    params:
        config["visualize_guides"],
    output:
        html="results/visualize_guides/report.html",
    conda:
        "envs/visualize_guides.yml"
    log:
        path="results/visualize_guides/log.txt",
    script:
        "notebooks/report.Rmd"


# module to convert HTML to PDF output
# -----------------------------------------------------
rule pdf:
    input:
        html=rules.visualize_guides.output.html,
    output:
        pdf="results/pdf/report.pdf",
    conda:
        "envs/pdf.yml"
    log:
        path="results/pdf/log.txt",
    shell:
        "weasyprint -v {input.html} {output.pdf} &> {log.path}"


# module to combine all module log files to single log
# -----------------------------------------------------
rule module_logs:
    input:
        rules.get_genome.log.path,
        rules.bowtie_index.log.path,
        rules.design_guides.log.path,
        rules.visualize_guides.log.path,
        rules.pdf.log.path,
    conda:
        "envs/basic.yml"
    log:
        path="results/module_logs/log.txt",
    shell:
        "cat {input} >> {log.path}"


# module to fetch software versions from conda envs
# -----------------------------------------------------
rule versions:
    input:
        expand(
            wfpath("envs/{module}"),
            module=listdir(wfpath("envs")),
        ),
    output:
        path="results/versions/log_packages.txt",
    conda:
        "envs/basic.yml"
    log:
        path="results/versions/log_env.txt",
    shell:
        "conda env export > {log.path};"
        "cat {input} >> {output.path}"
